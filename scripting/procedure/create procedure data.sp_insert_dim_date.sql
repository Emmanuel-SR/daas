CREATE PROCEDURE DATA.SP_INSERT_DIM_DATE (@CURRENT_DATE AS DATE) AS

BEGIN

  BEGIN TRY

    INSERT INTO DATA.DIM_DATE
      (
        DATE_KEY,
        FULL_DATE_ALTERNATE_KEY,
        CALENDAR_YEAR,
        CALENDAR_YEAR_MONTH,
        CALENDAR_SEMESTER_OF_YEAR,
        CALENDAR_QUARTER_OF_YEAR,
        CALENDAR_SEMESTER_OF_YEAR_DESCRIPTION,
        CALENDAR_QUARTER_OF_YEAR_DESCRIPTION,
        MONTH_NUMBER_OF_YEAR,
        WEEK_NUMBER_OF_MONTH,
        WEEK_NUMBER_OF_MONTH_DESCRIPTION,
        DAY_NUMBER_OF_MONTH,
        SPANISH_MONTH_NAME
      )
   VALUES
     (
        (DATEPART(YEAR , @CURRENT_DATE) * 10000) + (DATEPART(MONTH , @CURRENT_DATE) * 100) + DATEPART(DAY , @CURRENT_DATE),
        @CURRENT_DATE,
        DATEPART(YEAR , @CURRENT_DATE),
        LEFT(CONVERT(CHAR, @CURRENT_DATE, 112), 6),
        CASE WHEN DATEPART(QUARTER, @CURRENT_DATE) < 3 THEN 1 ELSE 2 END,
        DATEPART(QUARTER, @CURRENT_DATE),
        CASE WHEN DATEPART(QUARTER, @CURRENT_DATE) < 3 THEN 'S1' ELSE 'S2' END,
        CONCAT('T',DATEPART(QUARTER, @CURRENT_DATE)),
        DATEPART(MONTH, @CURRENT_DATE),
        CASE
          WHEN DATEPART(day,@CURRENT_DATE) <  8 THEN '1'
          WHEN DATEPART(day,@CURRENT_DATE) < 15 THEN '2'
          WHEN DATEPART(day,@CURRENT_DATE) < 22 THEN '3'
          WHEN DATEPART(day,@CURRENT_DATE) < 29 THEN '4'
          ELSE '5'
        END,
        CASE
          WHEN DATEPART(day,@CURRENT_DATE) <  8 THEN 'SEM1'
          WHEN DATEPART(day,@CURRENT_DATE) < 15 THEN 'SEM2'
          WHEN DATEPART(day,@CURRENT_DATE) < 22 THEN 'SEM3'
          WHEN DATEPART(day,@CURRENT_DATE) < 29 THEN 'SEM4'
          ELSE 'SEM5'
        END,
        DATEPART(DAY, @CURRENT_DATE),
        DATENAME(MONTH, @CURRENT_DATE)
     );

  END TRY

  BEGIN CATCH

    DECLARE @ERROR_MESSAGE NVARCHAR(4000);
    DECLARE @ERROR_SEVERITY INT;
    DECLARE @ERROR_STATE INT;

    SELECT
      @ERROR_MESSAGE = ERROR_MESSAGE(),
      @ERROR_SEVERITY = ERROR_SEVERITY(),
      @ERROR_STATE = ERROR_STATE();

    RAISERROR (@ERROR_MESSAGE, @ERROR_SEVERITY, @ERROR_STATE);

  END CATCH

END;