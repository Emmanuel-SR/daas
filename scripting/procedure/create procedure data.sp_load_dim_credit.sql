CREATE PROCEDURE DATA.SP_LOAD_DIM_CREDIT AS

BEGIN

  BEGIN TRY

    INSERT INTO DATA.DIM_CREDIT
      (
        PRODUCT_KEY,
        CREDIT_CODE,
        CLIENT_CODE,
        CLIENT_DOCN,
        CLIENT_NAME,
        PORTFOLIO_NAME
      )
    SELECT
      P.PRODUCT_KEY,
      S.CREDIT_CODE,
      S.CLIENT_CODE,
      S.CLIENT_DOCN,
      S.CLIENT_NAME,
      S.PORTFOLIO_NAME
     FROM (
            SELECT
              H.PRODUCT_NAME,
              H.CREDIT_CODE,
              H.CLIENT_CODE,
              H.CLIENT_DOCN,
              H.CLIENT_NAME,
              H.PORTFOLIO_NAME
            FROM [STAGE].[HIST_ASSIGNMENT] H
            GROUP BY H.PRODUCT_NAME, H.CREDIT_CODE, H.CLIENT_CODE, H.CLIENT_DOCN, H.CLIENT_NAME, H.PORTFOLIO_NAME
    ) S
    LEFT JOIN DATA.DIM_PRODUCT P ON P.PRODUCT_NAME = S.PRODUCT_NAME
    WHERE NOT EXISTS
          (
            SELECT 1
            FROM DATA.DIM_CREDIT D
            WHERE D.CREDIT_CODE = S.CREDIT_CODE
          );

  END TRY

  BEGIN CATCH

    DECLARE @ERROR_MESSAGE NVARCHAR(4000);
    DECLARE @ERROR_SEVERITY INT;
    DECLARE @ERROR_STATE INT;

    SELECT
      @ERROR_MESSAGE = ERROR_MESSAGE(),
      @ERROR_SEVERITY = ERROR_SEVERITY(),
      @ERROR_STATE = ERROR_STATE();

    RAISERROR (@ERROR_MESSAGE, @ERROR_SEVERITY, @ERROR_STATE);

  END CATCH

END;